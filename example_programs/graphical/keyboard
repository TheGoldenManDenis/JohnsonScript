constant screenwh 16;
option "vsize"  + 16 * 16 screenwh screenwh;
variable keyarray letter;
variable x y xp yp c xo yo;

set keyarray  alloc * screenwh screenwh 16;

startgraphics (* screenwh 16) (* screenwh 16);
_setxoyo;

_preparewithstring getref "Type stuff in here ";

while ! & mouseb 4;

 REM ------------- handle expose -------------
 if expose;
  bgcol 0 0 96;
  cls;
  _setxoyo;
  gcol 0;
  rectanglef xo yo (*screenwh 16);
 endif;

 REM ------------- read keys -----------------
 while keybuffer;
  set c  & readkey 255;
  if c;
   set D( + keyarray (* xp screenwh) yp )  | (<< 255 8) c;
   set xp + xp 1;
   if >= xp screenwh;
    set xp 0;
    set yp + yp 1;
    if >= yp screenwh;
     set yp 0;
    endif;
   endif;
  endif;
 endwhile;

 REM ------------- update graphics -----------
 set x 0;
 while < x screenwh;
  set y 0;
  while < y screenwh; 

   set letter  ( + keyarray (* x screenwh) y );
   if & Dletter (<< 255 8);
    processletter x y letter; 
   endif;

   set y + y 1;
  endwhile;
  set x + x 1;
 endwhile;

 wait 16.6;

endwhile;

stopgraphics;
wait 16;
return 0;



function processletter  x y letter  local  char bri;
 set char  &  Dletter 255;
 set bri   >> Dletter 8;

 set bri - bri 1;

 gcol bri bri bri;
 drawtext (+(* x 16)xo)
          (+(* y 16)yo)
          3
          (chr$ char);
 
 set Dletter  | (<< bri 8) char;
endfunction;

function _setxoyo;
 set xo  - (* winw 0.5) (* screenwh 8);
 set yo  - (* winh 0.5) (* screenwh 8);
endfunction;

function _preparewithstring  string  local i c;
 set i 0;
 while < i len$ $string;
  set c asc$ mid$ $string i 1;
  if = c 32;
   set xp  0;
   set yp  % (+ yp 1) screenwh;
  else
   set D( + keyarray (* xp screenwh) yp )  | (<< 255 8) c;
   set xp  + xp 1;
   set yp  + yp (= xp screenwh);
   set xp  % xp screenwh;
  endif;
  set i + i 1;
 endwhile;
endfunction;
